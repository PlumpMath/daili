apply plugin: 'java'
apply plugin: 'application'

mainClassName = 'org.github.taowen.daili.Main'
sourceCompatibility = 1.6
targetCompatibility = 1.6

repositories {
    mavenCentral()
    mavenLocal()
}

configurations {
    kilim
}

run {
    standardInput = System.in
    if ( project.hasProperty("appArgs") ) {
        args Eval.me(appArgs)
    }
}

jar {
    manifest { attributes 'Main-Class': 'org.github.taowen.daili.Main' }
}

test {
    testLogging.showStandardStreams = true
    systemProperty 'logback.configurationFile', 'logback.xml'
}

dependencies {
    testCompile 'dnsjava:dnsjava:2.1.6'
    compile 'org.slf4j:slf4j-api:1.7.+'
    compile 'org.github.taowen:kilim:1.0'
    kilim 'org.github.taowen:kilim:1.0'
    testCompile 'junit:junit:4.+'
    runtime 'ch.qos.logback:logback-classic:1.+'
}

task weaveMain(type: JavaExec) {
    classpath "$project.buildDir/classes/main"
    classpath configurations.kilim
    main = "kilim.tools.Weaver"
    args "-d", "$project.buildDir/classes/main", "$project.buildDir/classes/main"
}

task weaveTest(type: JavaExec) {
    classpath "$project.buildDir/classes/main"
    classpath "$project.buildDir/classes/test"
    classpath configurations.kilim
    classpath configurations.testCompile
    main = "kilim.tools.Weaver"
    args "-d", "$project.buildDir/classes/test", "$project.buildDir/classes/test"
}

classes.dependsOn weaveMain
testClasses.dependsOn weaveTest
